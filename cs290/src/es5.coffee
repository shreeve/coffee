# ==============================================================================
# ES5 Backend - Solar Directive Processor for CoffeeScript
# ==============================================================================
#
# Converts Solar directives (pure data) to CoffeeScript AST node instances
#
# Solar directives are generated by the parser from Solar syntax.coffee rules:
#   o 'IDENTIFIER', $ast: 'IdentifierLiteral', value: 1
#
# This backend converts those directives into actual CoffeeScript AST nodes:
#   new nodes.IdentifierLiteral(tokenValue)
#
# The resulting AST nodes use the existing CoffeeScript compiler pipeline.
# ==============================================================================

class ES5Backend
  constructor: (@options = {}, @ast = {}) ->
    @compileOptions =
      bare: @options.bare ? true
      header: @options.header ? false

  # Helper methods
  _stripQuotes: (value) ->
    return value unless value?.length >= 2 and typeof value is 'string'
    if (value[0] is '"' and value[value.length-1] is '"') or (value[0] is "'" and value[value.length-1] is "'")
      value.slice(1, -1)
    else
      value

  reduce: (values, positions, stackTop, symbolCount, directive) ->
    o = (index) -> values[stackTop - symbolCount + 1 + index]
    for own prop, value of directive
      continue if prop in ['name', 'length', 'prototype']
      o[prop] = value
    @resolve o

  resolve: (value, lookup = value) ->
    return lookup(value - 1) if typeof value is 'number' and lookup?
    return value if typeof value is 'number'
    return value if typeof value in ['string', 'boolean']
    return(value.map (item) => @resolve item, lookup) if Array.isArray value

    if value? and (typeof value is 'object' or typeof value is 'function')
      o = value
      return o if o.constructor?.name and o.constructor.name not in ['Object', 'Function'] # AST nodes
      useLookup = if typeof value is 'function' then value else lookup
      $ = (val) => @resolve val, useLookup  # Local resolver

      if o.$ast?
        nodeType = o.$ast

        switch nodeType
          when 'Root'              then new @ast.Root ((b) -> b.makeReturn(); b)(new @ast.Block $(o.body))
          when 'IdentifierLiteral' then new @ast.IdentifierLiteral $(o.value)
          when 'NumberLiteral'     then new @ast.NumberLiteral     $(o.value)
          when 'StringLiteral'     then new @ast.StringLiteral     @_stripQuotes($(o.value))
          when 'Value'             then new @ast.Value             $(o.val)
          when 'Assign'            then new @ast.Assign            $(o.variable), $(o.value)
          when 'Op'                then new @ast.Op                $(o.args[0]), $(o.args[1]), (if o.args[2]? then $(o.args[2]))
          when 'Literal'           then new @ast.Literal           $(o.value)
          else
            console.warn "ES5Backend: Unimplemented AST node type:", nodeType
            new @ast.Literal "/* Unimplemented: #{nodeType} */"

      else if o.$ary?
        items = $(o.$ary)
        if Array.isArray(items) then items else [items]

      else if o.$use?
        resolvedValue = $(o.$use)
        resolvedValue = resolvedValue[o.method]?() ? resolvedValue if o.method?
        resolvedValue

      else if o.$ops?
        console.warn "ES5Backend: $ops not yet implemented:", o.$ops
        new @ast.Literal "/* $ops: #{o.$ops} */"

      else
        console.warn "ES5Backend: Unknown directive:", o
        new @ast.Literal "/* Unknown directive */"

    else
      new @ast.Literal "/* Unexpected input */"

module.exports = ES5Backend
